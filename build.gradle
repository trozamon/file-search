plugins {
    id 'java'
    id 'application'
}

sourceCompatibility = '1.8'
targetCompatibility = '1.8'
applicationName = 'file-search'
mainClassName = 'com.alectenharmsel.indexer.Main'

repositories {
    jcenter()
}

dependencies {
    compile 'com.fasterxml.jackson.core:jackson-databind:2.9.9'
    compile 'com.google.guava:guava:20.0'
    compile 'io.vertx:vertx-core:3.8.0'
    compile 'io.vertx:vertx-web-templ-jade:3.8.0'
    compile 'io.vertx:vertx-web:3.8.0'
    compile 'org.apache.tika:tika-core:1.22'
    compile 'org.apache.tika:tika-parsers:1.22'
    compile 'org.elasticsearch.client:elasticsearch-rest-high-level-client:7.3.0'
}

task yarn(type: Exec) {
    commandLine 'yarn'
    args 'install'
    inputs.files('package.json', 'yarn.lock')
    outputs.dir('node_modules')
}

task copyJquery(type: Copy) {
    from 'node_modules/jquery/dist'
    include 'jquery.min.js'
    into 'build/resources/main/webroot'
}

task copySemantic(type: Copy) {
    from 'node_modules/semantic-ui-css'
    include 'semantic.min.*'
    include 'themes/**/*'
    into 'build/resources/main/webroot'
}

copyJquery.dependsOn yarn
copySemantic.dependsOn yarn

jar.dependsOn copyJquery
jar.dependsOn copySemantic

run {
    jvmArgs '-Dvertxweb.environment=dev'
    args 'example.config'
}
